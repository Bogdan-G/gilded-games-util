buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext.config = loadConfig(file('build.properties'))

version = config.mod_version
group = "com.gildedgames.util"
archivesBaseName = "GildedGamesUtil"

minecraft {
    version = config.forge_version
    mappings = config.forge_mappings

    runDir = "eclipse"
}

repositories {
    mavenCentral()
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea {
	module {
		inheritOutputDirs = true
	}
}

def loadConfig(File config) {
	config.withReader {
		def properties = new Properties()
		properties.load(it)

		return (new ConfigSlurper().parse(properties))
	}
}